# github.com/MadBomber/justprep/**/crystal/justfile
#
# brew install just
#   Handy way to save and run project-specific commands
#   https://github.com/casey/just
#


# List available recipes
@help:
  echo
  echo "Available Recipes at"
  echo `pwd`
  echo "are:"
  echo
  just -l --list-prefix 'just ' --list-heading ''
  echo


alias compile := build
release := "false"
extra_build_args := if join(os(), release) == "linux/true" { "--static" } else { "" }

# Builds the crystal version of justprep
build: _make_bin
  crystal build {{extra_build_args}} --no-debug --release -p -o bin/justprep justprep.cr version.cr


# Install the justprep executable in ~/bin
install where="/usr/local/bin": build
  cp bin/justprep {{where}}

#################################################
## Hidden Recipes


# Create the ./bin directory if it does not exist
_make_bin:
  #!/bin/bash
  if [[ ! -d ./bin ]] ; then
    mkdir ./bin
  fi
