# github.com/MadBomber/justprep/**/crystal/justfile
#
# brew install just
#   Handy way to save and run project-specific commands
#   https://github.com/casey/just
#

repo_root := "$PWD/.."


# List available recipes
@help:
  echo
  echo "Available Recipes at"
  echo `pwd`
  echo "are:"
  echo
  just -l --list-prefix 'just ' --list-heading ''
  echo

alias compile := build

# static builds only work on linux (and only if you have the dev libraries installed)
# default to not building static-ly but only if asked for and on linux
static := "false"
extra_build_args := if join(os(), static) == "linux/true" { "--static" } else { "" }

# Builds the crystal version of justprep
build: _make_bin install_shards
  crystal build {{extra_build_args}} --no-debug --release -p -o bin/justprep \
    {{repo_root}}/ruby/lib/justprep/common/*.crb \
    justprep.cr

# Run `shards install`
@install_shards:
  shards install

# Install the justprep executable in ~/bin
install where="/usr/local/bin": build
  sudo cp bin/justprep {{where}}

#################################################
## Hidden Recipes


# Create the ./bin directory if it does not exist
_make_bin:
  #!/bin/bash
  if [[ ! -d ./bin ]] ; then
    mkdir ./bin
  fi
