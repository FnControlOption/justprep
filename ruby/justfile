# github.com/MadBomber/justprep/**/ruby/justfile
#
# gem install bump

set positional-arguments := true

# List available recipes
@help:
  echo
  echo "Available Recipes at"
  echo `pwd`
  echo "are:"
  echo
  just -l --list-prefix 'just ' --list-heading ''
  echo


# Build the current gem
@build:
  rake clean clobber build


# Install the gem locally
@install:
  rake install:local


#################################################
## Recipes that deal with the source code version
## Version manager is handled by the "bump" gem

# Set the version: major . minor . patch
@set version:
  bump set {{version}} --no-commit --replace-in ../crystal/version.cr


# Show current version
@show:
  bump current


# Bump the level:  major . minor . patch
bump level='patch':
  #!/bin/bash
  if [[ "{{level}}" =~ ^(major|minor|patch)$ ]]; then
    bump {{level}} --no-commit --replace-in ../crystal/version.cr
  else
    echo "ERROR: level must be one of: major, minor, patch"
  fi
