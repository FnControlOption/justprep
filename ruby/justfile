# github.com/MadBomber/justprep/**/ruby/justfile
#

set positional-arguments := true

version := `cd $RR && semver format %M.%m.%p`

# List available recipes
@help:
  echo
  echo "Available Recipes at"
  echo `pwd`
  echo "are:"
  echo
  just -l --list-prefix 'just ' --list-heading ''
  echo

# Run the minitest examples on the Ruby code
test: build
  rake test


# Build the current gem
@build:
  rake clean clobber build


# Install the gem locally
@install:
  rake install:local


# Publish gem rubygems.org and to its own github repo as a package
publish:
  #!/usr/bin/env bash
  cd $RR
  direnv allow

  export PUSH_HOST=$RUBYGEMS_HOST
  rake build release

  export PUSH_HOST=$GITHUB_HOST
  rake build

  gem push --key github \
    --host https://rubygems.pkg.github.com/MadBomber \ 
    pkg/justprep-{{version}}.gem
